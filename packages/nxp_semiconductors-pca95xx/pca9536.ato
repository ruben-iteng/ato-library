#pragma experiment("BRIDGE_CONNECT")

import Electrical
import Capacitor
import ElectricLogic
import ElectricPower
import I2C

from "pca95xx.ato" import NXP_Semiconductors_PCA95xx_base_driver
from "parts/NXP_Semicon_PCA9536D_118/NXP_Semicon_PCA9536D_118.ato" import NXP_Semicon_PCA9536D_118_package
from "parts/NXP_Semicon_PCA9536TK_118/NXP_Semicon_PCA9536TK_118.ato" import NXP_Semicon_PCA9536TK_118_package
from "parts/NXP_Semicon_PCA9536DP_118/NXP_Semicon_PCA9536DP_118.ato" import NXP_Semicon_PCA9536DP_118_package


module NXP_Semiconductors_PCA9536_driver from NXP_Semiconductors_PCA95xx_base_driver:
    """
    NXP Semiconductors PCA9536
    - 4-bit I2C-bus GPIO
    - Operating power supply voltage range of 2.3 V to 5.5 V
    - 5 V tolerant I/Os
    - Polarity Inversion register
    - Low standby current
    - Noise filter on SCL/SDA inputs
    - No glitch on power-up
    - Internal power-on reset
    - 4 I/O pins which default to 4 inputs with 100 kâ„¦ internal pull-up resistor
    - 0 Hz to 400 kHz clock frequency
    - ESD protection exceeds 2000 V HBM per JESD22-A114, 200 V MM per JESD22-A115 and 1000 V CDM per JESD22-C101
    - Latch-up testing is done to JEDEC Standard JESD78 which exceeds 100 mA

    3 package options:
    - HVSON8
    - TSSOP8
    - S08
    """

    # external interfaces (not in base driver)
    io = new ElectricLogic[4]

    # decoupling capacitor
    decoupling_capacitor = new Capacitor
    power.vcc ~> decoupling_capacitor ~> power.gnd
    decoupling_capacitor.value = 100nF +/- 10%
    decoupling_capacitor.package = "C0402"

    # parameters
    i2c.address = 0x41


module NXP_Semiconductors_PCA9536D_driver from NXP_Semiconductors_PCA9536_driver:
    """
    NXP Semiconductors PCA9536D SO-8 package
    """
    package = new NXP_Semicon_PCA9536D_118_package

    # connections to package
    package.SCL ~ i2c.scl.line
    package.SDA ~ i2c.sda.line
    package.VDD ~ power.hv
    package.VSS ~ power.lv
    package.IO0 ~ io[0].line
    package.IO1 ~ io[1].line
    package.IO2 ~ io[2].line
    package.IO3 ~ io[3].line


module NXP_Semiconductors_PCA9536TK_driver from NXP_Semiconductors_PCA9536_driver:
    """
    NXP Semiconductors PCA9536TK HVSON-8 package
    """
    package = new NXP_Semicon_PCA9536TK_118_package

    # connections to package
    package.SCL ~ i2c.scl.line
    package.SDA ~ i2c.sda.line
    package.VDD ~ power.hv
    package.VSS ~ power.lv
    package.IO0 ~ io[0].line
    package.IO1 ~ io[1].line
    package.IO2 ~ io[2].line
    package.IO3 ~ io[3].line


module NXP_Semiconductors_PCA9536DP_driver from NXP_Semiconductors_PCA9536_driver:
    """
    NXP Semiconductors PCA9536DP TSSOP-8 package
    """
    package = new NXP_Semicon_PCA9536DP_118_package

    # connections to package
    package.SCL ~ i2c.scl.line
    package.SDA ~ i2c.sda.line
    package.VDD ~ power.hv
    package.VSS ~ power.lv
    package.IO0 ~ io[0].line
    package.IO1 ~ io[1].line
    package.IO2 ~ io[2].line
    package.IO3 ~ io[3].line
