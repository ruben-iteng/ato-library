import ElectricLogic
import ElectricPower
import Capacitor
import LDO

from "parts/TECH_PUBLIC_TPAP2112K_3_3TRG1/TECH_PUBLIC_TPAP2112K_3_3TRG1.ato" import TECH_PUBLIC_TPAP2112K_3_3TRG1_package

module LDO_5V_to_3V3:
    """5V to 3.3V 600mA (max 1100mA) LDO"""

    # external interfaces
    power_in = new ElectricPower
    power_out = new ElectricPower

    # components
    ldo = new TECH_PUBLIC_TPAP2112K_3_3TRG1_driver
    power_in_decoupling_capacitor = new Capacitor
    power_out_decoupling_capacitor = new Capacitor

    # connections
    power_in ~ ldo.power_in
    power_out ~ ldo.power_out

    power_in.hv ~ power_in_decoupling_capacitor.power.hv
    power_in.lv ~ power_in_decoupling_capacitor.power.lv
    power_out.hv ~ power_out_decoupling_capacitor.power.hv
    power_out.lv ~ power_out_decoupling_capacitor.power.lv

    ldo.enable.enable.line ~ power_in.hv

    # parameters
    power_in_decoupling_capacitor.value = 1uF +/- 20%
    power_in_decoupling_capacitor.package = "C0402"
    power_out_decoupling_capacitor.value = 1uF +/- 20%
    power_out_decoupling_capacitor.package = "C0402"

    power_in.required = True
    power_out.required = True


component TECH_PUBLIC_TPAP2112K_3_3TRG1_driver from LDO:
    """Basic 5.5V to 3.3V LDO nominal 600mA, max 1.1A with enable pin"""

    package = new TECH_PUBLIC_TPAP2112K_3_3TRG1_package

    # connections
    enable.enable.line ~ package.EN
    power_in.lv ~ package.GND
    power_in.hv ~ package.VIN
    power_out.hv ~ package.VOUT

    # parameters
    assert power_in.voltage within 1.2V to 5.5V
    dropout_voltage = 0.32V
    output_voltage = 3.3V +/- 0.2%
    ripple_rejection_ratio = 75dB to 80dB
    output_current = 601mA to 1100mA
    quiescent_current = 10uA
