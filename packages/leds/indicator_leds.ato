import LED
import Resistor
import ElectricPower


module LEDIndicator:
    power = new ElectricPower

    current_limiting_resistor = new Resistor
    led = new LED

    current_limiting_resistor.package = "R0402"

    current = 0.5mA to 3mA # TODO: calculate with brightness (TypicalLuminousIntensity) instead

    assert (power.voltage - led.forward_voltage) / current is current_limiting_resistor.resistance
    assert (power.voltage - led.forward_voltage) / current_limiting_resistor.resistance is current
    assert current_limiting_resistor.resistance * current is (power.voltage - led.forward_voltage)
    assert current <= led.max_current

    # Sanity check the voltage is within a safe range
    # for this module's design
    # FIXME: we can't ensure the power interface's forward voltage is set at all
    assert power.voltage <= 60V
    assert power.voltage >= led.forward_voltage

    power.hv ~ led.anode; led.cathode ~ current_limiting_resistor.p1; current_limiting_resistor.p2 ~ power.gnd


module LEDIndicatorRed from LEDIndicator:
    led -> _KT_0603R


module LEDIndicatorGreen from LEDIndicator:
    led -> _KT_0603G


module LEDIndicatorBlue from LEDIndicator:
    led -> _KT_0603B


module LEDIndicatorYellow from LEDIndicator:
    led -> _KT_0603Y


module LEDIndicatorYellowGreen from LEDIndicator:
    led -> _KT_0603YG


module LEDIndicatorWhite from LEDIndicator:
    led -> _KT_0603W


component _KT_0603B from LED:
    """Hubei KENTO Elec KT-0603B Blue LED"""
    lcsc_id = "C2288"
    forward_voltage = 2.9V +/- 10%
    max_current = 20mA +/- 20%
    # pins
    cathode ~ pin 1
    anode ~ pin 2


component _KT_0603G from LED:
    """Hubei KENTO Elec KT-0603G Green LED"""
    lcsc_id = "C12624"
    forward_voltage = 2.75V +/- 10%
    max_current = 20mA +/- 20%
    # pins
    cathode ~ pin 1
    anode ~ pin 2


component _KT_0603R from LED:
    """Hubei KENTO Elec KT-0603R Red LED"""
    lcsc_id = "C2286"
    forward_voltage = 2V +/- 10%
    max_current = 20mA +/- 20%
    # pins
    cathode ~ pin 1
    anode ~ pin 2


component _KT_0603Y from LED:
    """Hubei KENTO Elec KT-0603Y Yellow LED"""
    lcsc_id = "C2287"
    forward_voltage = 1.8V to 2.4V
    max_current = 25mA +/- 20%

    # pins
    anode ~ pin 1
    cathode ~ pin 2


component _KT_0603YG from LED:
    """Hubei KENTO Elec KT-0603YG Yellow-Green LED"""
    lcsc_id = "C2289"
    forward_voltage = 2.0V to 2.2V
    max_current = 20mA +/- 20%

    # pins
    cathode ~ pin 1
    anode ~ pin 2


component _KT_0603W from LED:
    """Hubei KENTO Elec KT-0603W White LED"""
    lcsc_id = "C2290"
    forward_voltage = 2.6V to 3.1V
    max_current = 30mA +/- 20%

    # pins
    anode ~ pin 1
    cathode ~ pin 2
