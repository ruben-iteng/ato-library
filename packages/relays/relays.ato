import ElectricPower
import ElectricSignal
import ElectricLogic
import Capacitor

from "ruben-iteng/leds/indicator_leds.ato" import LEDIndicatorRed
from "ruben-iteng/leds/indicator_leds.ato" import LEDIndicatorGreen


module SPST_Relay from Relay:
    """Single Pole Single Throw Relay"""
    input = new ElectricLogic

    common = new ElectricSignal
    contact = new ElectricSignal

module SPDT_Relay from Relay:
    """Single Pole Double Throw Relay"""
    input_a = new ElectricLogic

    common = new ElectricSignal
    normally_closed = new ElectricSignal
    normally_open = new ElectricSignal

module DPST_Relay from Relay:
    """Double Pole Single Throw Relay"""
    input_a = new ElectricLogic
    input_b = new ElectricLogic

    common_a = new ElectricSignal
    contact_a = new ElectricSignal

    common_b = new ElectricSignal
    contact_b = new ElectricSignal

module DPDT_Relay from Relay:
    """Double Pole Double Throw Relay"""
    input_a = new ElectricLogic
    input_b = new ElectricLogic

    common_a = new ElectricSignal
    normally_closed_a = new ElectricSignal
    normally_open_a = new ElectricSignal

    common_b = new ElectricSignal
    normally_closed_b = new ElectricSignal
    normally_open_b = new ElectricSignal

module Relay:
    coil = new ElectricPower

module DrivenDPDTBistableRelay:
    """
    Bistable DPDT relay with driver and indicator LEDs
    - 5V coil power
    - 5V/3.3V logic power
    - 2A contact carry current
    - TC118S DC motor driver (H-bridge) as relay driver
    """

    # interfaces
    power = new ElectricPower
    assert power.voltage within 5V+/-10%

    input_a = new ElectricLogic
    input_b = new ElectricLogic

    common_a = new ElectricSignal
    normally_closed_a = new ElectricSignal
    normally_open_a = new ElectricSignal

    common_b = new ElectricSignal
    normally_closed_b = new ElectricSignal
    normally_open_b = new ElectricSignal

    # components
    relay = new G6KU2FYTRDC5
    relay_driver = new Shenzhen_Fuman_Elec_TC118S_driver
    led_indicator_a = new LEDIndicatorRed
    led_indicator_b = new LEDIndicatorGreen

    # connections
    power ~ relay_driver.power
    input_a.reference.lv ~ power.lv
    input_b.reference.lv ~ power.lv

    relay_driver.input_a ~ input_a
    led_indicator_a.power.hv ~ input_a.line
    led_indicator_a.power.lv ~ input_a.reference.lv
    relay_driver.input_b ~ input_b
    led_indicator_b.power.hv ~ input_b.line
    led_indicator_b.power.lv ~ input_b.reference.lv
    relay_driver.motor_interface ~ relay.coil

    relay.common_a ~ common_a
    relay.normally_closed_a ~ normally_closed_a
    relay.normally_open_a ~ normally_open_a

    relay.common_b ~ common_b
    relay.normally_closed_b ~ normally_closed_b
    relay.normally_open_b ~ normally_open_b


module DrivenDPDTBistableRelayPushTerminal:
    """
    DrivenDPDTBistableRelay with push terminal
    """

    # external interfaces
    power = new ElectricPower

    input_a = new ElectricLogic
    input_b = new ElectricLogic

    # components
    relay = new DrivenDPDTBistableRelay
    screw_terminal = new _Cixi_Kefa_Elec_KF250T_5_0_23P

    # connections
    relay.power ~ power
    relay.input_a ~ input_a
    relay.input_b ~ input_b

    relay.normally_closed_a.line ~ screw_terminal.PA1
    relay.common_a.line ~ screw_terminal.PA2
    relay.normally_open_a.line ~ screw_terminal.PA3

    relay.normally_closed_b.line ~ screw_terminal.PB1
    relay.common_b.line ~ screw_terminal.PB2
    relay.normally_open_b.line ~ screw_terminal.PB3

component G6KU2FYTRDC5 from DPDT_Relay:
    """Omron Electronics G6KU_2F_Y_TR_DC5 bistable relay"""
    lcsc_id = "C2153173"
    manufacturer = "Omron Electronics"
    mpn = "G6KU-2F-Y-TR DC5"
    datasheet_url = "https://wmsc.lcsc.com/wmsc/upload/file/pdf/v2/lcsc/2211281630_Omron-Electronics-G6KU-2F-Y-TR-DC5_C2153173.pdf"
    designator_prefix = "RELAY"

    assert coil.voltage within 5V+/-10%
    coil.vcc ~ pin 8
    coil.gnd ~ pin 1

    normally_closed_a.line ~ pin 2
    common_a.line ~ pin 3
    normally_open_a.line ~ pin 4

    normally_closed_b.line ~ pin 7
    common_b.line ~ pin 6
    normally_open_b.line ~ pin 5

module Shenzhen_Fuman_Elec_TC118S_driver:
    """
    Shenzhen Fuman Elec TC118S dc motor driver
    - Single-channel built-in high-power MOS full-bridge driver
    - Supports forward, reverse, stop, and brake functions
    - Built-in efficient thermal response and thermal protection
    - Low on-resistance (1.6Î©)
    - Maximum continuous output current: 1.8A, peak current: 2.5A
    """
    # components
    package = new Shenzhen_Fuman_Elec_TC118S_package
    decoupling_capacitors = new Capacitor[2]
    motor_filter_capacitor = new Capacitor

    # external interfaces
    power = new ElectricPower
    motor_interface = new ElectricPower
    input_a = new ElectricLogic
    input_b = new ElectricLogic

    # connections to package
    power.vcc ~ package.VDD
    power.gnd ~ package.PGND
    power.gnd ~ package.AGND
    motor_interface.vcc ~ package.OUTA
    motor_interface.gnd ~ package.OUTB
    input_a.line ~ package.INA
    input_b.line ~ package.INB

    # decoupling capacitors
    decoupling_capacitors[0].power ~ power
    decoupling_capacitors[0].capacitance = 100nF +/- 20%
    decoupling_capacitors[0].package = "C0402"
    decoupling_capacitors[1].power ~ power
    decoupling_capacitors[1].capacitance = 4.7uF +/- 20%
    decoupling_capacitors[1].max_voltage = 14V to 25V
    motor_filter_capacitor.power ~ motor_interface
    motor_filter_capacitor.capacitance = 100pF +/- 20%
    motor_filter_capacitor.package = "C0402"

    # parameters
    assert power.voltage <= 7V

component Shenzhen_Fuman_Elec_TC118S_package:
    """Shenzhen Fuman Elec TC118S package"""
    lcsc_id = "C88308"
    manufacturer = "Shenzhen Fuman Elec"
    mpn = "TC118S"
    datasheet_url = "https://www.lcsc.com/datasheet/lcsc_datasheet_2204121200_Shenzhen-Fuman-Elec-TC118S_C88308.pdf"
    designator_prefix = "U"

    # pins
    signal AGND ~ pin 6
    signal INA ~ pin 2
    signal INB ~ pin 3
    signal OUTA ~ pin 8
    signal OUTB ~ pin 5
    signal PGND ~ pin 7
    signal VDD ~ pin 4

component _Cixi_Kefa_Elec_KF250T_5_0_23P:
    """Double layer push terminal 2x3 pin"""
    lcsc_id = "C976537"
    manufacturer = "Cixi Kefa Elec"
    mpn = "KF250T-5.0-2*3P"
    datasheet_url = "https://www.lcsc.com/datasheet/lcsc_datasheet_2303170930_Cixi-Kefa-Elec-KF250T-5-0-2-3P_C976537.pdf"
    designator_prefix = "J"

    # pins
    signal PA1 ~ pin 1
    signal PA2 ~ pin 3
    signal PA3 ~ pin 5

    signal PB1 ~ pin 2
    signal PB2 ~ pin 4
    signal PB3 ~ pin 6
