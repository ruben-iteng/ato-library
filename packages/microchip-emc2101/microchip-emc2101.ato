#pragma experiment("BRIDGE_CONNECT")

import BJT
import Capacitor
import Electrical
import ElectricLogic
import ElectricPower
import I2C
import Resistor

from "ruben-iteng/connectors/fan_connectors.ato" import Fan_4p_Connector
from "ruben-iteng/connectors/fan_connectors.ato" import Fan_Interface

from "parts/Microchip_Tech_EMC2101_R_ACZL_TR/Microchip_Tech_EMC2101_R_ACZL_TR.ato" import Microchip_Tech_EMC2101_R_ACZL_TR_package
from "parts/KEC_Semicon_2N3906S_RTK_PS/KEC_Semicon_2N3906S_RTK_PS.ato" import KEC_Semicon_2N3906S_RTK_PS_package


module Microchip_EMC2101_driver:
    """
    MicrochipEMC2101 fan driver
    Automatic Beta Compensation
    - Resistance Error Correction
    - Self-programming with available SMBus compatible EEPROM
    - Selectable PWM or DAC fan driver output
    - Temperature Monitors
      - External channel ±1°C accuracy
      - Internal channel ±2°C accuracy
    - 3.3 Volt Operation (5 Volt Tolerant Input Buffers)
    - SMBus 2.0 Compliant Interface, supports TIMEOUT
    """

    # components
    package = new Microchip_Tech_EMC2101_R_ACZL_TR_package
    decoupling_capacitor = new Capacitor
    tach_pullup_resistor = new Resistor
    pwm_pullup_resistor = new Resistor
    temperature_sensor = new _KEC_Semicon_2N3906S_RTK_PS
    temperature_sensor_decoupling_capacitor = new Capacitor

    # external interfaces
    fan_interface = new Fan_Interface
    power = new ElectricPower
    i2c = new I2C

    # connections to package
    package.nALERT_TACH ~ fan_interface.tach.line
    package.DN ~ temperature_sensor.collector
    package.DP ~ temperature_sensor.emitter
    package.FAN ~ fan_interface.pwm.line
    package.GND ~ power.lv
    package.SMCLK ~ i2c.scl.line
    package.SMDATA ~ i2c.sda.line
    package.VDD ~ power.hv

    # connections
    power ~ decoupling_capacitor.power
    package.DN ~> temperature_sensor_decoupling_capacitor ~> package.DP

    fan_interface.tach.line ~> tach_pullup_resistor ~> power.hv
    fan_interface.pwm.line ~> pwm_pullup_resistor ~> power.hv

    temperature_sensor.base ~ temperature_sensor.collector
    power.lv ~ fan_interface.power.lv

    # parameters
    decoupling_capacitor.capacitance = 100nF +/- 10%
    decoupling_capacitor.package = "C0402"
    temperature_sensor_decoupling_capacitor.capacitance = 2.2nF +/- 10%
    temperature_sensor_decoupling_capacitor.package = "C0402"
    tach_pullup_resistor.resistance = 10kohm +/- 1%
    tach_pullup_resistor.package = "R0402"
    pwm_pullup_resistor.resistance = 10kohm +/- 1%
    pwm_pullup_resistor.package = "R0402"

    assert power.voltage within 3.3V +/- 10%
    i2c.address = 0x4C

    power.required = True
    i2c.required = True


component _KEC_Semicon_2N3906S_RTK_PS from BJT:
    """_2N3906S PNP transistor"""
    package = new KEC_Semicon_2N3906S_RTK_PS_package

    # connections to package
    collector ~ package.C
    base ~ package.B
    emitter ~ package.E
