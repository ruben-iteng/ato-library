#pragma experiment("BRIDGE_CONNECT")

import BJT
import Capacitor
import Electrical
import ElectricLogic
import ElectricPower
import I2C
import Resistor

from "ruben-iteng/connectors/fan_connectors.ato" import Fan_4p_Connector, Fan_Interface


module Microchip_EMC2101_driver:
    """
    MicrochipEMC2101 fan driver
    Automatic Beta Compensation
    - Resistance Error Correction
    - Self-programming with available SMBus compatible EEPROM
    - Selectable PWM or DAC fan driver output
    - Temperature Monitors
      - External channel ±1°C accuracy
      - Internal channel ±2°C accuracy
    - 3.3 Volt Operation (5 Volt Tolerant Input Buffers)
    - SMBus 2.0 Compliant Interface, supports TIMEOUT
    """

    # components
    package = new Microchip_Tech_EMC2101_R_ACZL_TR_package
    decoupling_capacitor = new Capacitor
    tach_pullup_resistor = new Resistor
    pwm_pullup_resistor = new Resistor
    temperature_sensor = new _KEC_Semicon_2N3906S_RTK_PS
    temperature_sensor_decoupling_capacitor = new Capacitor

    # external interfaces
    fan_interface = new Fan_Interface
    power = new ElectricPower
    i2c = new I2C

    # connections to package
    package.ALERTh_TACH ~ fan_interface.tach.line
    package.DN ~ temperature_sensor.collector
    package.DP ~ temperature_sensor.emitter
    package.FAN ~ fan_interface.pwm.line
    package.GND ~ power.lv
    package.SMCLK ~ i2c.scl.line
    package.SMDATA ~ i2c.sda.line
    package.VDD ~ power.hv

    # connections
    power ~ decoupling_capacitor.power
    package.DN ~> temperature_sensor_decoupling_capacitor ~> package.DP

    fan_interface.tach.line ~> tach_pullup_resistor ~> power.hv
    fan_interface.pwm.line ~> pwm_pullup_resistor ~> power.hv

    temperature_sensor.base ~ temperature_sensor.collector
    power.lv ~ fan_interface.power.lv

    # parameters
    decoupling_capacitor.capacitance = 100nF +/- 10%
    decoupling_capacitor.package = "C0402"
    temperature_sensor_decoupling_capacitor.capacitance = 2.2nF +/- 10%
    temperature_sensor_decoupling_capacitor.package = "C0402"
    tach_pullup_resistor.resistance = 10kohm +/- 1%
    tach_pullup_resistor.package = "R0402"
    pwm_pullup_resistor.resistance = 10kohm +/- 1%
    pwm_pullup_resistor.package = "R0402"

    assert power.voltage within 3.3V +/- 10%
    i2c.address = 0x4C

    power.required = True
    i2c.required = True


component Microchip_Tech_EMC2101_R_ACZL_TR_package:
    """Microchip_Tech_EMC2101_R_ACZL_TR package"""
    lcsc_id = "C626968"
    manufacturer = "Microchip Tech"
    mpn = "EMC2101-R-ACZL-TR"
    datasheet_url = "https://wmsc.lcsc.com/wmsc/upload/file/pdf/v2/lcsc/2210180200_Microchip-Tech-EMC2101-R-ACZL-TR_C626968.pdf"
    designator_prefix = "U"

    # pins
    signal ALERTh_TACH ~ pin 6
    signal DN ~ pin 3
    signal DP ~ pin 2
    signal FAN ~ pin 4
    signal GND ~ pin 5
    signal SMCLK ~ pin 8
    signal SMDATA ~ pin 7
    signal VDD ~ pin 1


component _KEC_Semicon_2N3906S_RTK_PS from BJT:
    """_2N3906S PNP transistor"""
    package = new _KEC_Semicon_2N3906S_RTK_PS_package

    # connections to package
    collector ~ package.C
    base ~ package.B
    emitter ~ package.E


component _KEC_Semicon_2N3906S_RTK_PS_package:
    """KEC_Semicon_2N3906S_RTK_PS component"""
    lcsc_id = "C5300004"
    manufacturer = "KEC Semicon"
    mpn = "2N3906S-RTK/PS"
    datasheet_url = "https://wmsc.lcsc.com/wmsc/upload/file/pdf/v2/lcsc/2212060930_KEC-Semicon-2N3906S-RTK-PS_C5300004.pdf"
    designator_prefix = "Q"

    # pins
    signal B ~ pin 2
    signal C ~ pin 3
    signal E ~ pin 1
