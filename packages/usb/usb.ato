#pragma experiment("FOR_LOOP")
#pragma experiment("BRIDGE_CONNECT")

import USB2_0
import USB2_0_IF
import ElectricPower
import ElectricLogic
import ElectricSignal
import Electrical
import Resistor
import Fuse
import TECH_PUBLIC_USBLC6_2SC6
import Capacitor
import WCHJiangsu_Qin_Heng_CH224K

from "atopile/indicator-leds/indicator-leds.ato" import LEDIndicatorBlue

from "parts/BHFUSE_BSMD0805_050_15V/BHFUSE_BSMD0805_050_15V.ato" import BHFUSE_BSMD0805_050_15V_package
from "parts/Jing_Extension_of_the_Electronic_Co_918_418K2022Y40000/Jing_Extension_of_the_Electronic_Co_918_418K2022Y40000.ato" import Jing_Extension_of_the_Electronic_Co_918_418K2022Y40000_package
from "parts/SHOU_HAN_TYPE_C_16PIN_2MD_073/SHOU_HAN_TYPE_C_16PIN_2MD_073.ato" import SHOU_HAN_TYPE_C_16PIN_2MD_073_package

module USB_PD_Power_Sink_Vertical_Connector_driver from USB_PD_Power_Sink_driver:
    """
    USB PD power sink with Vertical Type-C connector

    Set the requested PD voltages as follows:
    9V  -> resistance = 6.8kohm
    12V -> resistance = 24kohm
    15V -> resistance = 56kohm
    20V -> resistance = DNP
    pd_sink = new USBPDPowerSink
    pd_sink.pd_trigger.voltage_set_resistor.resistance = 56kohm +/- 1%
    """
    connector -> Jing_Extension_of_the_Electronic_Co__918_418K2022Y40000_driver


module USB_PD_Power_Sink_Horizontal_Connector_driver from USB_PD_Power_Sink_driver:
    """
    USB PD power sink with Horizontal Type-C connector

    Set the requested PD voltages as follows:
    9V  -> resistance = 6.8kohm
    12V -> resistance = 24kohm
    15V -> resistance = 56kohm
    20V -> resistance = DNP
    pd_sink = new USBPDPowerSink
    pd_sink.pd_trigger.voltage_set_resistor.resistance = 56kohm +/- 1%
    """
    connector -> SHOU_HAN_TYPE_C_16PIN_2MD_073_driver


module USB_PD_Power_Sink_driver:
    """
    USB PD power sink with Type-C connector

    Set the requested PD voltages as follows:
    9V  -> resistance = 6.8kohm
    12V -> resistance = 24kohm
    15V -> resistance = 56kohm
    20V -> resistance = DNP
    pd_sink = new USBPDPowerSink
    pd_sink.pd_trigger.voltage_set_resistor.resistance = 56kohm +/- 1%

    Choose between vertical or horizontal connector:
    connector -> USB_TypeC_Vertical_Connector_driver
    connector -> USB_TypeC_Horizontal_Connector_driver
    """

    # external interfaces
    power = new ElectricPower
    power_good = new ElectricLogic

    # components
    connector = new USB_TypeC_Connector_driver
    pd_trigger = new WCH_CH224K_driver

    # connections
    connector.usb.usb_if.d ~ pd_trigger.usb_data.d
    connector.power_vbus ~ pd_trigger.vbus
    connector.cc[0] ~ pd_trigger.cc[0]
    connector.cc[1] ~ pd_trigger.cc[1]
    connector.shield ~ power.lv
    connector.power_vbus ~ power

    power.required = True

module WCH_CH224K_driver:
    """
    USB PD and Other Fast Charging Protocol Sink Controller Reference Design
    Design is ment for connecting to a female USB Type-C connector.

    Set the requested PD voltages as follows:
    9V  -> resistance = 6.8kohm
    12V -> resistance = 24kohm
    15V -> resistance = 56kohm
    20V -> resistance = DNP
    pd_trigger = new WCH_CH224K_driver
    pd_trigger.voltage_set_resistor.resistance = 56kohm +/- 1%
    """

    # external interfaces
    vbus = new ElectricPower
    power_good = new ElectricLogic
    usb_data = new USB2_0_IF
    cc = new ElectricSignal[2]

    # components
    controller = new WCHJiangsu_Qin_Heng_CH224K
    voltage_set_resistor = new Resistor
    vdd_resistor = new Resistor
    vbus_detect_resistor = new Resistor
    decoupling_capacitor = new Capacitor
    power_good_indicator = new LEDIndicatorBlue

    # connections
    vbus.hv ~> vbus_detect_resistor ~> controller.vbus_detect.line
    vbus.hv ~> vdd_resistor ~> controller.power.hv
    controller.power ~ decoupling_capacitor.power
    controller.power.lv ~ vbus.lv
    controller.power_good ~ power_good
    controller.power_good.line ~ power_good_indicator.power.lv
    power_good_indicator.power.hv ~ vbus.hv
    power_good_indicator.power.voltage = vbus.voltage
    controller.usb ~> usb_data.d
    controller.cc1 ~> cc[0]
    controller.cc2 ~> cc[1]
    controller.cfg[0].line ~> voltage_set_resistor ~> vbus.lv

    # parameters
    assert vbus.voltage within 4.5V to 20.5V
    vdd_resistor.resistance = 1kohm +/- 1%
    vdd_resistor.package = "R0402"
    vbus_detect_resistor.resistance = 10kohm +/- 1%
    vbus_detect_resistor.package = "R0402"
    decoupling_capacitor.capacitance = 1uF +/- 20%
    decoupling_capacitor.package = "C0402"
    #voltage_set_resistor.resistance = 10kohm +/- 1%
    voltage_set_resistor.package = "R0402"

    vbus.required = True
    usb_data.d.required = True
    for c in cc:
        c.required = True


module USB2_0_TypeC_Data_Power_Vertical_Connector_driver from USB2_0_TypeC_Data_Power_driver:
    """
    USB 2.0 Type-C connector
    - 5V PD compatible
    - ESD protection
    - Fuse [500mA(hold), 1A(trip)]
    - Vertical USB Type-C connector
    """
    # connections to package
    connector -> Jing_Extension_of_the_Electronic_Co__918_418K2022Y40000_driver


module USB2_0_TypeC_Data_Power_Horizontal_Connector_driver from USB2_0_TypeC_Data_Power_driver:
    """
    USB 2.0 Type-C connector
    - 5V PD compatible
    - ESD protection
    - Fuse [500mA(hold), 1A(trip)]
    - Horizontal USB Type-C connector
    """
    # connections to package
    connector -> SHOU_HAN_TYPE_C_16PIN_2MD_073_driver


module USB2_0_TypeC_Data_Power_driver:
    """
    USB 2.0 Type-C connector
    - 5V PD compatible
    - ESD protection
    - Fuse [500mA(hold), 1A(trip)]

    Choose between vertical or horizontal connector
    connector -> Jing_Extension_of_the_Electronic_Co__918_418K2022Y40000_driver
    connector -> SHOU_HAN_TYPE_C_16PIN_2MD_073_driver
    """

    # external interfaces
    usb = new USB2_0
    power = new ElectricPower

    # components
    connector = new USB_TypeC_Connector_driver
    cc_resistors = new Resistor[2]
    esd_protection = new TECH_PUBLIC_USBLC6_2SC6
    esd_decoupling_capacitor = new Capacitor
    fused_usb = new FusedUSB2_0

    # connections
    power ~ usb.usb_if.buspower
    connector.usb ~ fused_usb.usb_in
    fused_usb.usb_out ~> esd_protection ~> usb
    connector.power_vbus ~ connector.usb.usb_if.buspower
    connector.shield ~ usb.usb_if.buspower.lv

    connector.cc[0].line ~> cc_resistors[0] ~> power.lv
    connector.cc[1].line ~> cc_resistors[1] ~> power.lv

    esd_protection.usb_in.usb_if.buspower ~ esd_decoupling_capacitor.power

    # parameters
    for res in cc_resistors:
        res.resistance = 5.1kohm +/- 1%
        res.package = "R0402"

    esd_decoupling_capacitor.capacitance = 100nF +/- 10%
    esd_decoupling_capacitor.package = "C0402"

    power.required = True


module FusedUSB2_0:
    """Connects two USB2_0 interfaces with a fuse in between"""

    usb_in = new USB2_0
    usb_out = new USB2_0
    fuse = new BHFUSE_BSMD0805_050_15V_driver

    # connections
    usb_in.usb_if.buspower.hv ~> fuse ~> usb_out.usb_if.buspower.hv
    usb_in.usb_if.buspower.lv ~ usb_out.usb_if.buspower.lv
    usb_in.usb_if.d ~ usb_out.usb_if.d


module USB_TypeC_Connector_driver:
    """USB type-C connector driver"""
    # external interfaces
    usb = new USB2_0
    power_vbus = new ElectricPower
    cc = new ElectricSignal[2]
    sbu = new ElectricSignal[2]
    shield = new Electrical


module SHOU_HAN_TYPE_C_16PIN_2MD_073_driver from USB_TypeC_Connector_driver:
    """Horizontal 14 pin USB type-C connector"""
    package = new SHOU_HAN_TYPE_C_16PIN_2MD_073_package

    # connections to package
    usb.usb_if.buspower.gnd ~ package.GND
    cc[0].line ~ package.CC1
    cc[1].line ~ package.CC2
    sbu[0].line ~ package.SBU1
    sbu[1].line ~ package.SBU2
    shield ~ package.SHELL
    usb.usb_if.d.p.line ~ package.DP1
    usb.usb_if.d.n.line ~ package.DN1
    usb.usb_if.d.p.line ~ package.DP2
    usb.usb_if.d.n.line ~ package.DN2
    power_vbus.hv ~ package.VBUS
    power_vbus.lv ~ package.GND


component Jing_Extension_of_the_Electronic_Co__918_418K2022Y40000_driver from USB_TypeC_Connector_driver:
    """Vertical 14 pin USB type-C connector"""
    package = new Jing_Extension_of_the_Electronic_Co_918_418K2022Y40000_package

    # connections to package
    usb.usb_if.buspower.gnd ~ package.G
    cc[0].line ~ package.CC1
    cc[1].line ~ package.CC2
    shield ~ package.0
    usb.usb_if.d.p.line ~ package.Dpos
    usb.usb_if.d.n.line ~ package.Dneg
    power_vbus.hv ~ package.V
    power_vbus.lv ~ package.G


component BHFUSE_BSMD0805_050_15V_driver from Fuse:
    package = new BHFUSE_BSMD0805_050_15V_package

    # parameters
    trip_current = 1A to 1.1A

    # pins
    unnamed[0] ~ package.1
    unnamed[1] ~ package.2
