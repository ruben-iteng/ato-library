#pragma experiment("FOR_LOOP")
#pragma experiment("BRIDGE_CONNECT")

import USB2_0
import USB2_0_IF
import ElectricPower
import ElectricLogic
import ElectricSignal
import Electrical
import Resistor
import Fuse
import TECH_PUBLIC_USBLC6_2SC6
import Capacitor
import WCHJiangsu_Qin_Heng_CH224K

from "atopile/indicator-leds/indicator-leds.ato" import LEDIndicatorGreen

module USB_PD_Power_Sink:
    """
    USB PD power sink with Type-C connector

    Set the requested PD voltages as follows:
    9V  -> resistance = 6.8kohm
    12V -> resistance = 24kohm
    15V -> resistance = 56kohm
    20V -> resistance = DNP
    pd_sink = new USBPDPowerSink
    pd_sink.pd_trigger.voltage_set_resistor.resistance = 56kohm +/- 1%
    """

    # external interfaces
    power = new ElectricPower
    power_good = new ElectricLogic

    # components
    connector = new USB_TypeC_16Pin_Connector_driver
    pd_trigger = new WCH_CH224K_driver

    # connections
    connector.usb.usb_if.d ~ pd_trigger.usb_data.d
    connector.power_vbus ~ pd_trigger.vbus
    connector.cc[0] ~ pd_trigger.cc[0]
    connector.cc[1] ~ pd_trigger.cc[1]
    connector.shield ~ power.lv
    connector.power_vbus ~ power

    power.required = True

module WCH_CH224K_driver:
    """
    USB PD and Other Fast Charging Protocol Sink Controller Reference Design
    Design is ment for connecting to a female USB Type-C connector.

    Set the requested PD voltages as follows:
    9V  -> resistance = 6.8kohm
    12V -> resistance = 24kohm
    15V -> resistance = 56kohm
    20V -> resistance = DNP
    pd_trigger = new WCH_CH224K_driver
    pd_trigger.voltage_set_resistor.resistance = 56kohm +/- 1%
    """

    # external interfaces
    vbus = new ElectricPower
    power_good = new ElectricLogic
    usb_data = new USB2_0_IF
    cc = new ElectricSignal[2]

    # components
    controller = new WCHJiangsu_Qin_Heng_CH224K
    voltage_set_resistor = new Resistor
    vdd_resistor = new Resistor
    vbus_detect_resistor = new Resistor
    decoupling_capacitor = new Capacitor
    power_good_indicator = new LEDIndicatorGreen

    # connections
    vbus.hv ~> vbus_detect_resistor ~> controller.vbus_detect.line
    vbus.hv ~> vdd_resistor ~> controller.power.hv
    controller.power ~ decoupling_capacitor.power
    controller.power.lv ~ vbus.lv
    controller.power_good ~ power_good
    controller.power_good ~ power_good_indicator.power.lv
    power_good_indicator.power.hv ~ vbus.hv
    power_good_indicator.power.voltage = vbus.voltage
    controller.usb ~> usb_data.d
    controller.cc1 ~> cc[0]
    controller.cc2 ~> cc[1]
    controller.cfg[0].line ~> voltage_set_resistor ~> vbus.lv

    # parameters
    vdd_resistor.resistance = 1kohm +/- 1%
    vdd_resistor.package = "R0402"
    vbus_detect_resistor.resistance = 10kohm +/- 1%
    vbus_detect_resistor.package = "R0402"
    decoupling_capacitor.capacitance = 1uF +/- 20%
    decoupling_capacitor.package = "C0402"
    #voltage_set_resistor.resistance = 10kohm +/- 1%
    voltage_set_resistor.package = "R0402"

    vbus.required = True
    usb_data.required = True
    for c in cc:
        c.required = True


module USB2_0_TypeC_Data_Power:
    """
    USB 2.0 Type-C connector
    - 5V PD compatible
    - ESD protection
    - Fuse [500mA(hold), 1A(trip)]
    """

    # external interfaces
    usb = new USB2_0
    power = new ElectricPower

    # components
    connector = new USB_TypeC_16Pin_Connector_driver
    cc_resistors = new Resistor[2]
    esd_protection = new TECH_PUBLIC_USBLC6_2SC6
    esd_decoupling_capacitor = new Capacitor
    fused_usb = new FusedUSB2_0

    # connections
    power ~ usb.usb_if.buspower
    connector.usb ~ fused_usb.usb_in
    fused_usb.usb_out ~> esd_protection ~> usb
    connector.power_vbus ~ connector.usb.usb_if.buspower
    connector.shield ~ usb.usb_if.buspower.lv

    connector.cc[0].line ~> cc_resistors[0] ~> connector.GND
    connector.cc[1].line ~> cc_resistors[1] ~> connector.GND

    esd_protection.usb_in.usb_if.buspower ~ esd_decoupling_capacitor.power

    # parameters
    for res in cc_resistors:
        res.resistance = 5.1kohm +/- 1%
        res.package = "R0402"

    esd_decoupling_capacitor.capacitance = 100nF +/- 10%
    esd_decoupling_capacitor.package = "C0402"

    power.required = True

module FusedUSB2_0:
    """Connects two USB2_0 interfaces with a fuse in between"""

    usb_in = new USB2_0
    usb_out = new USB2_0
    fuse = new BHFUSE_BSMD0805_050_15V

    # connections
    usb_in.usb_if.buspower.hv ~> fuse ~> usb_out.usb_if.buspower.hv
    usb_in.usb_if.buspower.lv ~ usb_out.usb_if.buspower.lv
    usb_in.usb_if.d ~ usb_out.usb_if.d


module USB_TypeC_16Pin_Connector_driver from SHOU_HAN_TYPE_C_16PIN_2MD073_package:
    """16 pin USB type-C connector"""

    # external interfaces
    usb = new USB2_0
    power_vbus = new ElectricPower
    cc = new ElectricSignal[2]
    sbu = new ElectricSignal[2]
    shield = new Electrical

    # connections to package
    usb.usb_if.buspower.gnd ~ GND
    cc[0].line ~ CC1
    cc[1].line ~ CC2
    sbu[0].line ~ SBU1
    sbu[1].line ~ SBU2
    shield ~ SHELL
    usb.usb_if.d.p.line ~ DP1
    usb.usb_if.d.n.line ~ DN1
    usb.usb_if.d.p.line ~ DP2
    usb.usb_if.d.n.line ~ DN2
    power_vbus.hv ~ VBUS
    power_vbus.lv ~ GND


component SHOU_HAN_TYPE_C_16PIN_2MD073_package:
    """Common 16 pin USB type-C connector"""
    lcsc_id = "C2765186"
    manufacturer = "SHOU HAN"
    mpn = "TYPE-C 16PIN 2MD(073)"
    datasheet_url = "https://jlcpcb.com/api/file/downloadByFileSystemAccessId/8588920841703079936"
    designator_prefix = "USB"

    # pins
    signal CC1 ~ pin 4
    signal CC2 ~ pin 10
    signal DN1 ~ pin 7
    signal DN2 ~ pin 5
    signal DP1 ~ pin 6
    signal DP2 ~ pin 8
    signal GND ~ pin 1
    GND ~ pin 12
    signal SBU1 ~ pin 9
    signal SBU2 ~ pin 3
    signal SHELL ~ pin 13
    SHELL ~ pin 14
    signal VBUS ~ pin 2
    VBUS ~ pin 11


component BHFUSE_BSMD0805_050_15V from Fuse:
    """BHFUSE_BSMD0805_050_15V component"""
    lcsc_id = "C883109"
    manufacturer = "BHFUSE"
    mpn = "BSMD0805-050-15V"
    datasheet_url = "https://www.lcsc.com/datasheet/lcsc_datasheet_2305121423_BHFUSE-BSMD0805-050-15V_C883109.pdf"
    designator_prefix = "F"

    # parameters
    trip_current = 1A to 1.1A

    # pins
    unnamed[0] ~ pin 1
    unnamed[1] ~ pin 2
